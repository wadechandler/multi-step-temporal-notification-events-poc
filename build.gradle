// Root build file for the Notification POC
// Multi-module project: notification-common, notification-repository,
// notification-messaging, notification-app

plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    group = 'com.wadechandler.notification.poc'
    version = '0.1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs.add('--enable-preview')
    }

    tasks.withType(JavaExec).configureEach {
        jvmArgs('--enable-preview')
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        jvmArgs('--enable-preview')
    }
}
